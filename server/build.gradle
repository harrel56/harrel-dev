plugins {
    id 'java'
}

group = 'dev.harrel'
version = '0.0.1'

configurations {
    web
    output
}

repositories {
    mavenCentral()
}

java {
    sourceCompatibility JavaVersion.VERSION_21
    targetCompatibility JavaVersion.VERSION_21
}

def jsonSchemaVersion = '1.8.1'

dependencies {
    implementation 'org.tinylog:slf4j-tinylog:2.7.0'
    implementation 'org.tinylog:tinylog-impl:2.7.0'

    implementation 'io.javalin:javalin:6.2.0'
    implementation "dev.harrel:json-schema:$jsonSchemaVersion"
    implementation 'com.sanctionco.jmail:jmail:1.6.3'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'

    web project(path: ':web', configuration: 'output')
}

processResources {
    inputs.files(configurations.web)
    doLast {
        copy {
            from(configurations.web)
            into("${sourceSets.main.output.resourcesDir}/web")
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Json-Schema-Version': jsonSchemaVersion
    }
}

tasks.register('fatJar', Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': 'dev.harrel.App',
                   'Json-Schema-Version': jsonSchemaVersion,
                   'Image-Version': project.findProperty('imageVersion') ?: 'unknown'
    }
    from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

artifacts {
    output(fatJar)
}
