plugins {
    id 'java'
}

group = 'dev.harrel'
version = '0.0.1'
sourceCompatibility = '19'

configurations {
    web
    output
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'ch.qos.logback:logback-classic:1.4.7'
    implementation 'io.javalin:javalin:5.5.0'
    implementation 'dev.harrel:json-schema:1.1.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'

    web project(path: ':web', configuration: 'output')
}

processResources {
    inputs.files(configurations.web)
    doLast {
        copy {
            from(configurations.web)
            into("${sourceSets.main.output.resourcesDir}/web")
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('fatJar', Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': 'dev.harrel.App'
    }
    from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

artifacts {
    output(fatJar)
}
